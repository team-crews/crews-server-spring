import org.hidetake.gradle.swagger.generator.GenerateSwaggerUI

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.epages.restdocs-api-spec' version '0.18.4'
    id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.server'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

swaggerSources {
    sample {
        setInputFile(file("build/api-spec/openapi3.json"))
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // data
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'com.redis:lettucemod-spring:4.1.2'

    // crypto
    implementation 'org.springframework.security:spring-security-crypto'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // jwt
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.2'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.2'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.2'

    // aop
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    //stmp
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // thymeleaf
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // api docs
    swaggerUI 'org.webjars:swagger-ui:4.15.5'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.4'
    testImplementation 'com.epages:restdocs-api-spec-restassured:0.18.4'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'

    // logback
    implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'
    implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.1'
    implementation "ca.pjer:logback-awslogs-appender:1.6.0"
}

tasks.named('test') {
    useJUnitPlatform()
}

openapi3 {
    server = 'http://localhost:8080'
    title = 'Crews API'
    description = 'Crews API 명세서입니다.'
    version = '0.1.0'
    format = 'json'
}

tasks.withType(GenerateSwaggerUI).configureEach {
    dependsOn 'openapi3'
}

tasks.register('copySwaggerUI', Copy) {
    dependsOn 'generateSwaggerUISample'
    dependsOn(jar)
    dependsOn(resolveMainClassName)

    def generateSwaggerUISampleTask = tasks.named('generateSwaggerUISample', GenerateSwaggerUI).get()
        from("${generateSwaggerUISampleTask.outputDir}")
        into("build/resources/main/static/docs")
}

bootJar {
    dependsOn 'copySwaggerUI'
}
